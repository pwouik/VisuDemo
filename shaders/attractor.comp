#version 460 core
layout (local_size_x = 1024, local_size_y = 1, local_size_z = 1) in;
layout(r32i, binding = 1) uniform iimage2D depth;
layout(std140, binding = 1) uniform Matrices { // the list of mat4 attractors
    mat4 matrixArray[10];
    float weights[10];
};
layout(std430, binding = 3) buffer PointBuffer {          // SSBO points
    vec4 points[];
};
layout(r32i, binding = 4) uniform iimage2D jumpdist; //texture that store jump distances
uniform int randInt_seed;   //a random value per frame (between 0 and RAND_MAX, WARNING RAND_MAX depends on compiler !)
uniform int matrixPerAttractor; //number of matrices in array

uniform mat4 proj;
uniform mat4 view;
uniform uvec2 screen_size;
uniform vec3 camera;



const uint max_iter = 256;
const float EPSILON = 1e-4;
const float PI = 3.14159265358979;

//magic values
#define NBPTS 20000000 //20 000 000 points in memory, but can do math for a smaller number
#define MAX_FUNC_PER_ATTRACTOR 10 //magic value synced with matthracttor.hpp
//TODO Changed MAX_FUNC_PER_ATTRACTOR to 3 or 5 to lower useless memory usage


/*
Wether we should use main v1 or v2 is hightly debatable and seems to depends on GPU !
- on CY-PC, main 2 seems better (-30% perf for 4 times more point)
- on my RTX980 main 2 divide perf by 8 for some reasons

*/


void screenWriteCoord(vec4 pt, float dist_jump){
    //pt = vec4(normalize(pt.xyz),1);
    vec4 camera_space = view * pt;

    vec4 projected_point = proj * camera_space;
    if (projected_point.w != 0.0) {
        projected_point.xyz /= projected_point.w; // Perspective divide
    }
    if(projected_point.z>1.0){return;}

    vec2 screen_coord = (projected_point.xy + 1.0) * 0.5; // [-1,1] to [0,1]
    screen_coord *= vec2(screen_size); // Map to screen space (pixels) 
    int depth_int = int(projected_point.z*(1<<31));
    if(imageLoad(depth, ivec2(screen_coord)).r<depth_int){
        int jumpdist_int = int(dist_jump*(1<<31));
        imageStore(depth, ivec2(screen_coord), ivec4(depth_int));
        imageStore(jumpdist, ivec2(screen_coord), ivec4(jumpdist_int));
        //other metadata
    }
}

uint hash(uint x){
    x ^= x >> 17;
    x *= 0xed5ad4bbU;
    x ^= x >> 11;
    x *= 0xac4c1b51U;
    x ^= x >> 15;
    x *= 0x31848babU;
    x ^= x >> 14;
    return x;
}

//MAIN VERSION 1 : draw 1 generation (all reachable point from current pos)
void main() {
    uint idx = gl_GlobalInvocationID.x;
    if(idx >= NBPTS) return;

    vec4 point = points[idx];
    vec4 pts[MAX_FUNC_PER_ATTRACTOR];

    //update point coordinate with attractor
    if(matrixPerAttractor > 0) {        
        //get a unique seed per thread. 

        //compute all points to display. Note that 3<matrixPerAttractor<MAX_FUNC_PER_ATTRACTOR = 10
        for(int i =0; i < matrixPerAttractor; i++){
            pts[i] = matrixArray[i] * point;
        }

        float fake_rand = float(hash(idx^randInt_seed)%2048)/2048; 
        //jump to a random position
        for(int i = 0;i<matrixPerAttractor;i++){
            if(weights[i]>fake_rand){
                points[idx] = pts[i];
                break;
            }
        }

    }
    //draw all points
    for(int i =0; i < matrixPerAttractor; i++){
        screenWriteCoord(pts[i],distance(pts[i], point));
    }
}

//MAIN VERSION 2 : draw 2 generation (all points reachable point from the first generation)
//tends to be way more laggy. Draws 4 times more points (with arguably better dispersion because it's consecutive generation ?). Consider lowering NBPTS when using this main
void main_v2() {
    uint idx = gl_GlobalInvocationID.x;
    if(idx >= NBPTS) return;

    vec4 point = points[idx];
    vec4 pts_gen1[MAX_FUNC_PER_ATTRACTOR];
    vec4 pts_gen2[MAX_FUNC_PER_ATTRACTOR*MAX_FUNC_PER_ATTRACTOR];

    //update point coordinate with attractor
    if(matrixPerAttractor > 0) {

        //compute all points to display. Note that 3<matrixPerAttractor<MAX_FUNC_PER_ATTRACTOR = 10
        for(int i =0; i < matrixPerAttractor; i++){
            pts_gen1[i] = matrixArray[i] * point;
            for(int k=0; k<matrixPerAttractor; k++){
                pts_gen2[i*matrixPerAttractor+k] = matrixArray[k] * pts_gen1[i];
            }
        }
        float fake_rand = float(hash(idx^randInt_seed)%2048)/2048;
        int new_pt;
        for(int i = 0;i<matrixPerAttractor;i++){
            if(weights[i]>fake_rand){
                new_pt;
                break;
            }
        }
        fake_rand = float(hash(idx^randInt_seed^0x1F54C5)%2048)/2048;
        for(int i = 0;i<matrixPerAttractor;i++){
            if(weights[i]>fake_rand){
                points[idx] = pts_gen2[new_pt*matrixPerAttractor+i];
                break;
            }
        }
    }
    //draw all reachable points form gen1 & gen2.
    for(int i =0; i < matrixPerAttractor; i++){
        screenWriteCoord(pts_gen1[i],distance(pts_gen1[i], point));
    }
    for(int i =0; i < matrixPerAttractor*matrixPerAttractor; i++){
        screenWriteCoord(pts_gen2[i],distance(pts_gen2[i], point));
    }
}